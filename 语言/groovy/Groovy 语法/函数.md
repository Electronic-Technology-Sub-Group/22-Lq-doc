- 可使用 `def` 声明函数，返回值类型自动推断（相当于 `Object`）
# 省略语法

- 函数调用时，若需要参数，括号可省略，参数与函数名之间以空格分割
	- 若一个函数没有任何参数，括号不可省略
	- 若是一个函数的参数是另一个函数的结果，需要加括号防止歧义

```groovy
static int add(int a) {
    return a + 1
}

static void main(String[] args) {
    int b = add 10
    println b
    // add 10 加括号防止歧义
    println (add 10)
}
```

- 参数最后一个为闭包时，闭包参数可以在括号外

```groovy
static int exec(int a, Closure<Integer> op) {
    return op(a)
}

static void main(String[] args) {
    int r = exec(10) {
        return it * 2
    }

    println r
}
```

- 函数最后一行就是返回值时，可省略 `return`

```groovy
static void main(String[] args) {
    println hello()
}

def hello() {
    "Hello"
}
```

- 允许省略异常声明 
# 函数重载

Groovy 重载函数选择由运行时的实际类型决定，而不是编译时决定

```groovy
int method(String arg) { return 1; }
int method(Object arg) { return 2; }

Object o = "object";
// Java：2
// Groovy：1
i = method(o); 
```

- Java：`i = 2`，编译器认为 `o` 类型为 `Object`
- Groovy：`i = 1`，运行时 `o` 类型为 `String`
# 具名参数

当方法第一个参数类型为 `Map` 时，允许使用具名参数，且具名参数位置可以在任意位置

```groovy
static void func(Map args) {
    println "msg=$args.message, arg=$args.argument"
}
static void func(Map args, int value, String pre) {
    println "[$pre:$value]msg=$args.message, arg=$args.argument"
}

func(message: 'Msgs', argument: 'Args')
func(message: 'Msg', argument: 'Arg', 5, 'aaa')
func(5, 'bbb', message: 'Msg', argument: 'Arg')
func(5, message: 'Msg', argument: 'Arg', 'bbb')
```
# 默认参数

参数可以有默认值

```groovy
static void fun(int a = 100, int b, int c = 1000, int d) {
    println a + b + c + d
}
// 1108：a=100，b=3，c=1000，d=5
fun(3, 5)
// 1009：a=1，b=3，c=1000，d=5
fun(1 ,3, 5)
// 11：a=1，b=2，c=3，d=5
fun(1, 2, 3, 5)
```
